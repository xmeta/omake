#
# Configuration utilities
#
# \begin{doc}
# \chapter{Autoconfiguration functions and variables}
# \label{chapter:autoconf}
# \cutname{omake-autoconf.html}
# \OMake{} standard library provides a number of functions and variables intended to help one write
# build specifications that need to be capable of autoconfiguring itself to adjust to different
# build environments.
#
# \section{General-purpose autoconfiguration functions}
# The following general-purpose functions can be used to discover the properties of your build
# environment in a fashion similar to the one used by GNU autoconf tool you may be familiar with.
# It is recommended that these function be used from an appropriate \verb+static.+ block (see
# Section~\ref{section:static.} for more information).
#
# In order to use the following general-purpose functions, you need to have the line
# \begin{verbatim}
# open configure/Configure
# \end{verbatim}
# included in your \verb+OMakefile+ or \verb+OMakeroot+.
# \end{doc}

#
# A number of helper functions, using the autoconf names, when appropriate.
#
# \begin{doc}
# \twofunslabel{AC_MSG_CHECKING}{AC\_MSG\_CHECKING}{AC_MSG_RESULT}{AC\_MSG\_RESULT}
# \begin{verbatim}
# AC_MSG_CHECKING(<msg>)
# ...
# AC_MSG_RESULT(<msg>)
# \end{verbatim}
# The \verb+AC_MSG_CHECKING+ function output message of the form \verb+--- Checking <msg>... +
# \emph{without} any trailing newline. After the test advertized by \verb+AC_MSG_CHECKING+ is
# performed, the \verb+AC_MSG_RESULT+ function should be used to output the result.
#
# In certain cases users may want to redefine these function --- for example, to use a different
# output formatting and/or to copy the messages to a log file.
#
# Example:
# \begin{verbatim}
# static. =
#    AC_MSG_CHECKING(which foo to use)
#    foo = ...
#    AC_MSG_RESULT($(foo))
# \end{verbatim}
# \end{doc}
#
AC_MSG_CHECKING(msg) =
    print($"--- Checking $(msg)... ")

AC_MSG_RESULT(msg) =
    println($"($(msg))")

# \begin{doc}
# \twofunslabel{AC_MSG_WARN}{AC\_MSG\_WARN}{AC_MSG_ERROR}{AC\_MSG\_ERROR}
# \begin{verbatim}
# AC_MSG_WARN(<msg>)
# AC_MSG_ERROR(<msg>)
# \end{verbatim}
# 
# Print a warning or an error message respectively. \verb+AC_MSG_ERROR+ would then abort \OMake.
# \end{doc}
AC_MSG_WARN(msg) =
    msg[] = $(split $(nl), $"$(msg)")
    print($(concat $(EMPTY), $(add-wrapper $'--- *** ', $(nl), $(msg))))

AC_MSG_ERROR(msg) =
    msg[] = $(split $(nl), $"$(msg)")
    eprintln($"""*** ERROR: $(concat '$(nl)---        ', $(msg))""")
    exit(1)
  
#
# \begin{doc}
# \twofunslabel{AC_MSG_YES_NO}{AC\_MSG\_YES\_NO}{AC_MSG_FOUND}{AC\_MSG\_FOUND}
# \begin{verbatim}
# flag = $(AC_MSG_YES_NO <bool expr>
# flag = $(AC_MSG_FOUND <bool expr>
# \end{verbatim}
#
# The \verb+AC_MSG_FOUND+ function expects to receive a boolean flag describing whether a test
# previously announced using the \hyperfunx{AC_MSG_CHECKING}{AC\_MSG\_CHECKING} found what it
# was looking for. \verb+AC_MSG_FOUND+ will output the appropriate result (``found'' or ``NOT found'')
# using the \hyperfunx{AC_MSG_RESULT}{AC\_MSG\_RESULT} and return its argument back.
#
# The \verb+AC_MSG_YES_NO+ function is similar, outputting a simple (``yes'' or ``NO'').
# \end{doc}
#
AC_MSG_FOUND(found) =
   AC_MSG_RESULT($(if $(found), found, NOT found))
   return $(found)
   
AC_MSG_YES_NO(found) =
   AC_MSG_RESULT($(if $(found), yes, NO))
   return $(found)

# \begin{doc}
# \threefunslabel{AC_TRY_COMPILE}{AC\_TRY\_COMPILE}{AC_TRY_LINK}{AC\_TRY\_LINK}{AC_TRY_RUN}{AC\_TRY\_RUN}
# \begin{verbatim}
# success = $(AC_TRY_COMPILE <prog_text>)
# success = $(AC_TRY_LINK <prog_text>)
# success = $(AC_TRY_RUN <prog_text>)
# \end{verbatim}
#
# Given the \emph{text} of a C program, the \verb+AC_TRY_COMPILE+, \verb+AC_TRY_LINK+, and \verb+AC_TRY_RUN+
# functions would try to compile / compile and link / compile, link, and run, the given program and return a boolean flag
# indicating whether the attempt was successful.
#
# \verb+AC_TRY_COMPILE+ will use the \hypervarn{CC}, \hypervarn{CFLAGS} and \hypervarn{INCLUDES} variables 
# to run the C compiler. \verb+AC_TRY_LINK+ and \verb+AC_TRY_RUN+ will also use the \hypervar{LDFLAGS}
# to run the C compiler and linker.
#
# These functions are silent and should normally be used with an appropriate
# \hyperfunxn{AC_MSG_CHECKING}{AC\_MSG\_CHECKING} $\ldots$ \hyperfunxn{AC_MSG_RESULT}{AC\_MSG\_RESULT}.
# \end{doc}

AC_TRY_COMPILING(command, prog, extra) =
    # The command line
    tmp = $(tmpfile omake)
    command += -o $(tmp) $(tmp).c

    # The program
    program = $"""/* Configuration file; you can remove this. */
/* Command line: $(command) */
$(prog)
"""
    
    # Compile it
    fprint($(tmp).c, $(program))
    protected.result = $(shell-success-null $(command))

    if $(result)
        switch $(extra)
        case Runs
            result = $(shell-success-null $(file $(tmp)$(EXE)))
            export
        case Output
            result =
                try
                    value $(shell $(file $(tmp)$(EXE)))
                default
                    value $(not true)
            export
        export

    # Remove temporaries
    rm -f $(tmp).c $(tmp)$(EXT_OBJ) $(tmp)$(EXE)

    return $(result)

AC_TRY_COMPILE(prog) =
    return $(AC_TRY_COMPILING $(CC) $(CFLAGS) $(PREFIXED_INCLUDES) -c, $(prog), None)

AC_TRY_LINK(prog) =
    return $(AC_TRY_COMPILING $(CC) $(CFLAGS) $(PREFIXED_INCLUDES) $(LDFLAGS), $(prog), None)

AC_TRY_RUN(prog) =
    return $(AC_TRY_COMPILING $(CC) $(CFLAGS) $(PREFIXED_INCLUDES) $(LDFLAGS), $(prog), Runs)

# \begin{doc}
# \funlabel{AC_RUN_PROG}{AC\_RUN\_PROG}
# \begin{verbatim}
# output = $(AC_RUN_PROG <prog>)
# \end{verbatim}
#
# \verb+AC_RUN_PROG+ is similar to the \hyperfunxn{AC_RUN_PROG}{AC\_RUN\_PROG}, except that it
# returns the output of the function (will return \verb+false+ if the program fails to compile
# or run).
# \end{doc}

AC_RUN_PROG(prog) =
    return $(AC_TRY_COMPILING $(CC) $(CFLAGS) $(PREFIXED_INCLUDES) $(LDFLAGS), $(prog), Output)

#
# Check whether a header file exists.
# We call the C compiler.
#
# \begin{doc}
# \twofuns{CheckHeader}{VerboseCheckHeader}
# \begin{verbatim}
# success = $(CheckHeader <files>)
# success = $(VerboseCheckHeader <files>)
# \end{verbatim}
#
# Use the \hyperfunx{AC_TRY_COMPILE}{AC\_TRY\_COMPILE} to check whether your C compiler can locate
# and process the specified headers files.
# Will incude \verb+<stdio.h>+ before including the header files.
#
# Both functions return a boolean value. The \verb+CheckHeader+ function is silent; the
# \verb+VerboseCheckHeader+ function will use the \hyperfunxn{AC_MSG_CHECKING}{AC\_MSG\_CHECKING} and
# \hyperfunxn{AC_MSG_RESULT}{AC\_MSG\_RESULT} functions to describe the test and the outcome.
#
# Example:
# \begin{verbatim}
# static. =
#    NCURSES_H_AVAILABLE = $(VerboseCheckHeader ncurses.h)
# \end{verbatim}
# \end{doc}
#
public.CheckHeader(files) =
    return $(AC_TRY_COMPILE $"""
#ifdef __cplusplus
extern "C"
#endif
#include <stdio.h>
$(add-wrapper $(nl)$'#include <', >, $(files))
int main(int argc, char **argv) {
    return 0;
}
""")

public.VerboseCheckHeader(files) =
    AC_MSG_CHECKING(for $(files))
    return $(AC_MSG_FOUND $(CheckHeader $(files)))

#
# Check whether the libraries have the given functions
#
# \begin{doc}
# \twofuns{CheckLib}{VerboseCheckLib}
# \begin{verbatim}
# success = $(CheckLib <libs>, <functions>)
# success = $(VerboseCheckLib <libs>, <functions>)
# \end{verbatim}
#
# Use the \hyperfunx{AC_TRY_LINK}{AC\_TRY\_LINK} to check whether your C compiler and linker can
# find the named functions when linking with the named libraries.  Will pass the \verb+<libs>+ to
# the compiler using the \verb+-l+ flag.
#
# Both functions return a boolean value. The \verb+CheckLib+ function is silent; the
# \verb+VerboseCheckHeader+ function will use the \hyperfunxn{AC_MSG_CHECKING}{AC\_MSG\_CHECKING} and
# \hyperfunxn{AC_MSG_RESULT}{AC\_MSG\_RESULT} functions to describe the test and the outcome.
#
# Example:
# \begin{verbatim}
# static. =
#     NCURSES_LIB_AVAILABLE = $(VerboseCheckLib ncurses, initscr setupterm tigetstr)
# \end{verbatim}
# \end{doc}
#
public.CheckLib(libs, funs) =
    CFLAGS += $(addprefix -l, $(libs))

    return $(AC_TRY_LINK $"""
#ifdef __cplusplus
extern "C"
#endif
/* Override any gcc2 internal prototype to avoid an error.  */
$(add-wrapper $(nl)extern char , $'();', $(funs))
int main(int argc, char **argv) {
    /* Usage */
$(add-wrapper $(nl)    , $'();', $(funs))
    return 0;
}
""")

public.VerboseCheckLib(libs, funs) =
    msg = $"""function$(if $(gt $(length $(funs)), 1), s) $(concat $", ", $(funs))"""
    msg =
        if $(libs)
            value $"""$(msg) in librar$(if $(gt $(length $(libs)), 1), ies, y) $(concat $", ", $(libs))"""
        else
            value $(msg)
    AC_MSG_CHECKING(for $(msg))
    return $(AC_MSG_FOUND $(CheckLib $(libs), $(funs)))

#
# Check whether a program exists in the PATH
#
# \begin{doc}
# \fun{CheckProg}
# \verb+success = $(CheckProg <prog>)+
#
# Checks whether the program \verb+<prog>+ exists in your path. Will use the
# \hyperfunxn{AC_MSG_CHECKING}{AC\_MSG\_CHECKING} and
# \hyperfunxn{AC_MSG_RESULT}{AC\_MSG\_RESULT} functions to describe the test and the outcome.
#
# \end{doc}
#
public.CheckProg(prog) =
   AC_MSG_CHECKING(for $(prog))
   WHERE = $(where $(prog))
   if $(WHERE)
      AC_MSG_RESULT(found $(nth 0, $(WHERE)))
      return true
   else
      AC_MSG_RESULT(FAILED - no $(prog) found)
      return false

#
# \begin{doc}
# \section{Predefined configuration tests}
# A number of configuration tests are already included in the standard library.
# In order to use them in your project, simply \verb+open+ (see Section~\ref{section:include}) the
# corresponding build file in your \verb+OMakefile+ and the tests will run the first time \OMake{}
# is executed. Note that it is not a problem to \verb+open+ these files from more than one place in
# your project --- if you do that, the test will still run only once.
# \end{doc}
#
